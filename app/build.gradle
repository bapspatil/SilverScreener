apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric' // Fabric's Crashlytics Gradle Plugin

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.1'
    defaultConfig {
        applicationId "bapspatil.silverscreener"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 60
        versionName "one.three.two"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'TMDB_API_TOKEN', MyTheMovieDBApiToken
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86_64', 'arm64-v8a'
            universalApk false
        }
    }

    project.ext.versionCodes = ['x86_64': 0, 'arm64-v8a': 1]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride = project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1 + android.defaultConfig.versionCode
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

ext {
    constraintVer = '1.1.3'
    glideVer = '4.9.0'
    butterknifeVer = '10.1.0'
    floatingsearchviewVer = '2.1.1'
    retrofitVer = '2.6.0'
    gsonVer = '2.8.5'
    cookiebar2Ver = '1.1.3'
    rvAnimVer = '3.0.0'
    expandTextVer = '1.0.5'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'com.google.android.material:material:1.1.0-alpha08'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta01'

    implementation "com.github.bumptech.glide:glide:$glideVer"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVer"
    implementation "com.jakewharton:butterknife:$butterknifeVer"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVer"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVer"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVer"
    implementation "com.google.code.gson:gson:$gsonVer"
    implementation "org.aviran.cookiebar2:cookiebar2:$cookiebar2Ver"
    implementation "jp.wasabeef:recyclerview-animators:$rvAnimVer"
    implementation "at.blogc:expandabletextview:$expandTextVer"
    implementation "com.github.arimorty:floatingsearchview:$floatingsearchviewVer"

    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1' // Firebase Crashlytics
    implementation "com.google.firebase:firebase-core:17.0.1" // Firebase Analytics
    implementation "com.google.firebase:firebase-messaging:19.0.1" // Firebase Cloud Messaging
}

apply plugin: 'com.google.gms.google-services' // Google Services Gradle Plugin