apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric' // Fabric's Crashlytics Gradle Plugin

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "bapspatil.silverscreener"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 60
        versionName "one.three.two"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'TMDB_API_TOKEN', MyTheMovieDBApiToken
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }

    project.ext.versionCodes = ['armeabi-v7a': 0, 'arm64-v8a': 1, 'x86': 2, 'x86_64': 3]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride = project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1 + android.defaultConfig.versionCode
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    google()
    jcenter()
}

ext {
    supportVer = '27.0.2'
    glideVer = '4.4.0'
    butterknifeVer = '8.8.1'
    materialsearchviewVer = '1.4.0'
    retrofitVer = '2.3.0'
    gsonVer = '2.8.2'
    cookiebar2Ver = '1.0.5'
    rvAnimVer = '2.2.7'
    expandTextVer = '1.0.5'
    firebase = '11.8.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:appcompat-v7:$supportVer"
    implementation "com.android.support:support-v4:$supportVer"
    implementation "com.android.support:cardview-v7:$supportVer"
    implementation "com.android.support:design:$supportVer"
    implementation "com.android.support:recyclerview-v7:$supportVer"

    implementation "com.github.bumptech.glide:glide:$glideVer"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVer"
    implementation "com.jakewharton:butterknife:$butterknifeVer"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVer"
    implementation "com.miguelcatalan:materialsearchview:$materialsearchviewVer"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVer"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVer"
    implementation "com.google.code.gson:gson:$gsonVer"
    implementation "org.aviran.cookiebar2:cookiebar2:$cookiebar2Ver"
    implementation "jp.wasabeef:recyclerview-animators:$rvAnimVer"
    implementation "at.blogc:expandabletextview:$expandTextVer"

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') { transitive = true } // Firebase Crashlytics
    implementation "com.google.firebase:firebase-core:$firebase" // Firebase Analytics
    implementation "com.google.firebase:firebase-messaging:$firebase" // Firebase Cloud Messaging
}

apply plugin: 'com.google.gms.google-services' // Google Services Gradle Plugin